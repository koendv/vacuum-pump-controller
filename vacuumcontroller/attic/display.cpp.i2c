#include "display.h"
#include "Fonts/Veranda24ptDigits.h"
#include "motor.h"
#include "sensor.h"
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <Wire.h>

/*
 * Display pressure and motor pwm on an 128x64 oled with ssd1306 controller.
 */

// i2c connection
#define OLED_SCL PB8
#define OLED_SDA PB9
#define SCREEN_WIDTH 128 // OLED display width, in pixels
#define SCREEN_HEIGHT 64 // OLED display height, in pixels
#define OLED_RESET -1

namespace display {

Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);
bool detected = false;
uint8_t i2c_address = 0;
int indent_negative_number = 0; // width of '0' - width of '-', 13 in Veranda24pt
uint32_t lastMillis = 0;        // timestamp display update
int last_vacuum = -1;           // values at last display update
int last_pwm_bargraph = -1;
sensor::state_enum last_state[NUM_SENSOR] = {sensor::NO_SENSOR};

// calculate how much to indent negative numbers so the digits of positive
// and negative numbers align.

void inline calcIndent() {
  oled.setCursor(0, 0);
  oled.print('0');
  indent_negative_number = oled.getCursorX();
  oled.setCursor(0, 0);
  oled.print('-');
  indent_negative_number -= oled.getCursorX();
  if (indent_negative_number < 0)
    indent_negative_number = 0;
}

// print number i right-aligned on pixel row y.
void drawRightInt(int32_t i, uint8_t y) {
  if (i >= 0)
    oled.setCursor(0, y);
  else
    oled.setCursor(indent_negative_number, y);
  // right align number
  oled.setTextColor(SSD1306_BLACK);
  if ((i < 1000) && (i > -100))
    oled.print('0');
  if ((i < 100) && (i > -10))
    oled.print('0');
  if ((i < 10) && (i > -1))
    oled.print('0');
  oled.setTextColor(SSD1306_WHITE);
  oled.print(i);
}

void setup() {
  // autodetect whether display on i2c address 0x3c or 0x3d
  Wire.setSDA(OLED_SDA);
  Wire.setSCL(OLED_SCL);
  Wire.begin();
  i2c_address = 0x3c;
  Wire.beginTransmission(i2c_address);
  detected = Wire.endTransmission() == 0;
  if (!detected) {
    i2c_address = 0x3d;
    Wire.beginTransmission(i2c_address);
    detected = Wire.endTransmission() == 0;
    if (!detected)
      return; // not found
  }
  // display found. initialize.
  oled.begin(SSD1306_SWITCHCAPVCC, i2c_address);
  oled.clearDisplay();
  oled.setFont(&Veranda24ptDigits);
  oled.setTextSize(1);
  oled.setTextColor(SSD1306_WHITE);
  calcIndent();
  oled.clearDisplay();
  oled.display();
  return;
}

void reset() {
  Wire.end();
  setup();
}

void loop() {

  if (!detected)
    return;
  int vacuum = sensor::vacuum;
  int pwm_bargraph = SCREEN_WIDTH * motor::pwma / MAXPWM;
  uint32_t now = millis();
  bool display_changed = (vacuum != last_vacuum) || (pwm_bargraph != last_pwm_bargraph);
  for (int i = 0; i < NUM_SENSOR; i++) {
    display_changed |= last_state[i] != sensor::state[i];
  }
  // check display needs updating
  if (((now < lastMillis) || (now - lastMillis > 500)) && display_changed) {
    // check for i2c hangs
    Wire.beginTransmission(i2c_address);
    detected = Wire.endTransmission() == 0;
    if (!detected) {
      Serial.println("oled?");
      return;
    }
    // bar graph of motor pwm
    oled.clearDisplay();
    oled.fillRect(0, 0, pwm_bargraph, 8, SSD1306_WHITE);
    // sensors
    for (int i = 0; i < NUM_SENSOR; i++) {
      // on a yellow/blue 128x64 oled, the first 16 rows are yellow, the last 48 blue
      const int16_t Y_YELLOW = 11;
      const int16_t Y_BLUE = 15;
      const int16_t bar_w = 20;
      const int16_t bar_h = 4;
      int16_t x = 30 * i + 5;

      switch (sensor::state[i]) {
      case sensor::NO_SENSOR:
        break;
      case sensor::SENSOR_OK:
        oled.fillRect(x, Y_BLUE, bar_w, bar_h, SSD1306_WHITE);
        break;
      case sensor::SENSOR_ERR:
        oled.fillRect(x, Y_YELLOW, bar_w, bar_h, SSD1306_WHITE);
        break;
      default:
        break;
      }
    }
    // print vacuum in hPa
    drawRightInt(vacuum, SCREEN_HEIGHT - 2); // print number right aligned
    // update display
    oled.display();
    // update status
    lastMillis = now;
    last_vacuum = vacuum;
    last_pwm_bargraph = pwm_bargraph;
    for (int i = 0; i < NUM_SENSOR; i++)
      last_state[i] = sensor::state[i];
  }
}
} // namespace display
// not truncated
